
- [操作系统真象还原](#操作系统真象还原)
  - [第 2 章](#第-2-章)
    - [计算机是怎么启动的](#计算机是怎么启动的)
    - [按下开机之后电脑发生了什么](#按下开机之后电脑发生了什么)
    - [启动 MAR](#启动-mar)
  - [第 3 章](#第-3-章)
    - [什么是地址](#什么是地址)

----------------

# 操作系统真象还原


## 第 2 章

### 计算机是怎么启动的

如果你亲自动手重载过系统你就听过 BIOS 这个名词。BIOS 全称叫 Base Input & Output System ,也就是基本输入输出系统。**它的主要作用就是检测、初始化硬件**（硬件自己本身就提供一些初始化的功能调用，BIOS 直接调用就可以了），当然还有非常重要的 **建立中断表**，接着通过 `Int` 中断来实现相关的硬件调用。其实这些操作对于硬件来说就是 IO 操作，但是由于空间只有 64kb 的局限，且受到 [实模式] 的局限，所以只需要保证硬件能运行基本的 IO 操作就行了。

> 接下来看看实模式下 1MB 内存的布局

![](https://cdn.jsdelivr.net/gh/kendall-cpp/blogPic@main/寻offer总结02/实模式内存布局.3fmzdzgtgye0.webp)


对于上表

- 0xffff0 为 BIOS 的入口地址，这里 16 字节的内容是跳转指令 jmp_f000: e05b
- 0xb8000 - 0xbffff 用于文本模式的显示适配器  
......


> 为什么插入主板的内存不是实际的全部内存

因为在计算机中，并不是只有插主板上的内存条需要通过地址总线，(地址总线的宽度决定可以访问的内存空间大小) 还有其他外设也是沟通过地址总线来访问内存的，所以地址总线需要预留出一些内存给外设（比如显存，硬盘控制器等），剩下的可用内存再指向 DRAM（动态随记访问内存）。这就是为什么安装了 4GB 的内存，而电脑中只显示 3.8GB 的原因

BIOS 是计算机上的第一个运行的软件，它是由硬件加载的，BIOS 的代码是写在 ROM [只读存储器] 上的 BIOS，因为 BIOS 代码所做的工作室一成不变的，不需要更改。 BIOS 执行的入口地址是 0xFFFF0。


### 按下开机之后电脑发生了什么

PC 机上电后的第一条指令就是 BIOS 固件中的，它负责检测和初始化 CPU、内存及主板平台，然后加载引导设备（大概率是硬盘）中的第一个扇区数据，到 0x7c00 地址开始的内存空间，再接着跳转到 0x7c00 处执行指令，在我们这里的情况下就是 GRUB 引导程序。



### 启动 MAR

创建一个文件 mbr.S ,将下面代码复制进去。

```
; mbr.S

; 主引导程序
; --------------------------------------------------
SECTION MBR vstart=0x7c00 ; 把起始地址编译为 0x7c00
    mov ax, cs     ; cs 代码段寄存器
    mov ds, ax     ; dx 数据段寄存器
    mov es, ax     ; es 附加段寄存器
    mov ss, ax     ; ss 堆栈段寄存器
    mov fs, ax     ; fs 80386 后添加的寄存器，无全称
    mov sp, 0x7c00 ; sp 堆栈指针寄存器

; 清屏
; --------------------------------------------------
; INT 0x10    功能号: 0x06    功能描述：上卷窗口
; --------------------------------------------------
; 输入：
; AH 功能号 = 0x06
; AL = 上卷的行数(如果为0，表示全部)
; BH = 上卷行属性
; (CL, CH) = 窗口左上角的 (X, Y) 位置
; (DL, DH) = 窗口右下角的 (X, Y) 位置
; 无返回值:
    mov ax, 0x600
    mov bx, 0x700
    mov cx, 0
    mov dx, 0x184f ; 右下角: (80, 25)
                   ; VGA 文本模式种，一行只能容纳 80 个字符，共 25 行
                   ; 下标从 0 开始，所以 0x18=24, 0x4f=79
        
    int 0x10       ; int 0x10

; .get_cursor 获取当前光标位置，在光标处打印字符
    mov ah, 3      ; 3 号子功能
    mov bh, 0      ; 待获取光标的页号

    int 0x10       ; 输出：
                   ; ch = 光标开始行，cl = 光标结束行
                   ; dh = 光标所在行号，dl = 光标所在列号

; 打印字符串
    mov ax, message
    mov bp, ax     ; es:bp 为串首地址
    
    mov cx, 5      ; cx 为串长度，不包括结束符 '\0'
    mov ax, 0x1301 ; 13 号子功能
                   ; ah = 13
                   ; al = 01: 写字符方式，显式字符串，光标跟随移动
    mov bx, 0x2    ; bh = 0，要显示的页号
                   ; bl = 02，字符属性，黑底绿字
    
    int 0x10

    jmp $          ; 程序悬停在此

    message db "1 MBR"    ; db 字节型
                          ; dw 字类型
                          ; dd 双字型
                          ; dq 四字型
                          ; dt 十字节型 
    times 510-($-$$) db 0 ; 填充文件末尾的魔数 0xaa55 和当前位置之间的空间
                          ; 保证编译后生成的文件大小为 512 字节（硬盘一个扇区的大小）
    db 0x55, 0xaa

```

然后编译 

nasm -o mbr.bin mbr.S

将编译后的 bin 文件写入扇区

```
kendall@kendall-virtual-machine:~/bochsken$ dd if=boot/mbr.bin of=hd60M.img bs=512 count=1 

conv=notrunc
1+0 records in
1+0 records out
512 bytes copied, 0.000185153 s, 2.8 MB/s
```

接下来通过 bochs 测试

![](https://cdn.jsdelivr.net/gh/kendall-cpp/blogPic@main/寻offer总结02/bochs-01.382j6t1cdm00.png)

------

## 第 3 章

### 什么是地址


